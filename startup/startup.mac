;**********************************************************************;
;                                                                      ;
;   This file is part of RSX180, a multi-tasking OS for the Z180.      ;
;   Copyright (C) 1985-2019, Hector Peraza.                            ;
;                                                                      ;
;   This program is free software; you can redistribute it and/or      ;
;   modify it under the terms of the GNU General Public License as     ;
;   published by the Free Software Foundation; either version 2 of     ;
;   the License, or (at your option) any later version.                ;
;                                                                      ;
;   This program is distributed in the hope that it will be useful,    ;
;   but WITHOUT ANY WARRANTY; without even the implied warranty of     ;
;   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the      ;
;   GNU General Public License for more details.                       ;
;                                                                      ;
;   You should have received a copy of the GNU General Public License  ;
;   along with this program; if not, write to the Free Software        ;
;   Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.          ;
;                                                                      ;
;**********************************************************************;

	.Z180

; Note: compile with ZSM4 (M80 reverses chars in 'CO' etc.)

Z80182	equ	1
	include	Z180.INC
	include	DCB.INC
	include	TCB.INC
	include	PCB.INC
	include	SYSFN.INC

	include	SYSCONF.INC

	extrn	SYSEND,SYSTOP,POOL,POOLSZ

	extrn	SYSSTK,KMINIT,MMUINI,INITKM,ININT,SETVEC,INTON,INTOFF
	extrn	TMRINT,DLOAD,ASSIGN,GO,TSTART,HOSTNM,SYSENT,SYSLVL
	extrn	KSIZE,CLRMEM,UPTIME,NVREAD,LDRTCB,NEWPCB

	extrn	INIT_S,INIT_E
	extrn	FSRV_S,FSRV_E
	extrn	LDR_S,LDR_E

	cseg

;-----------------------------------------------------------------------

; This is the main System Startup routine.
; Initializes everything and starts a few essential tasks before
; passing control to the dispatcher.

START::	di
	ld	sp,SYSSTK	; use SYSSTK in common memory

  IF 0	; HALT disables the internal Z182 DMA by default, which stalls disk
	; I/O.  In some early/buggy Z182 chips (like the one my P112 has)
	; the ENH182 register cannot be accessed or is not implemented at
	; all, and therefore DMA can't be enabled on HALT.  Thus, the
	; dipatcher will use a loop instead of a HALT instruction to wait
	; for an interrupt when there are no tasks ready.

	in0	a,(enh182)
	set	5,a
	out0	(enh182),a
  ENDIF

	ld	a,0F0h
	out0	(CBAR),a	; bank 0000-EFFF, common F000-FFFF
	in0	a,(BBR)
	out0	(CBR),a

	ld	hl,SYSEND	; get end of system image
	inc	hl
	inc	hl
	inc	hl
	ld	a,l
	and	0FCh		; ensure 4-byte alignment
	ld	l,a
	ld	(POOL),hl	; set start of dynamic storage area
	ex	de,hl
	ld	hl,0F000h	; top of system bank
	ld	(SYSTOP),hl
	or	a
	sbc	hl,de		; compute available kernel memory area
	ld	(POOLSZ),hl

	ld	hl,0
	ld	bc,128
	call	CLRMEM		; clear RST vector area

	ld	a,-1
	ld	(SYSLVL),a	; we are in system state

	call	NVREAD		; read machine-specific settings
	call	KMINIT		; init kernel memory allocator
	call	INITKM		; init task manager
	call	MMUINI		; init MMU
	call	ININT		; init interrupt system
	call	INTOFF		; make sure interrupts are off

	ld	de,TMRINT	; timer interrupt routine = int vector 2
	ld	a,2
	call	SETVEC

	ld	hl,UPTIME
	ld	bc,5
	call	CLRMEM		; init uptime

	ld	de,SYSPAR
	in0	a,(RAMLBR)
	ld	b,a
	ld	c,16		; first 64K reserved for system
	call	NEWPCB		; create SYSPAR partition
	;jr	c,...

	ld	de,LDRPAR
	ld	bc,0
	call	NEWPCB		; create LDRPAR partition
	;jr	c,...

	ld	de,FCPPAR
	ld	bc,0
	call	NEWPCB		; create FCPPAR partition
	;jr	c,...

	ld	de,GENPAR
	in0	a,(RAMLBR)
	add	a,16
	ld	b,a
	in0	a,(RAMUBR)
	inc	a
	sub	b
	ld	c,a		; use all remaining memory
	call	NEWPCB		; create GEN partition
	;jr	c,...
	set	PA.SYS,(ix+P.ATTR)

	ld	a,0C3h
	ld	(SYSRST),a	; for INIT task... in the future it will
	ld	hl,SYSENT	;  reside on its own bank
	ld	(SYSRST+1),hl

	ld	(0),a
	ld	hl,CHKTRP##
	ld	(1),hl		; setup illegal instruction trap for kernel

	ld	hl,dfhst
	ld	de,HOSTNM
	ld	bc,9
	ldir			; set default host name

	; Initialize physical device table with resident drivers

	call	INTON		; enable timer interrupts to service timeouts
	ei

	ld	hl,IDDTBL
ddloop:	ld	c,(hl)
	inc	hl
	ld	b,(hl)
	inc	hl
	ld	a,b
	or	c
	jr	z,done
	push	hl
	call	DLOAD		; install device driver (TODO: mark it as resident)
	pop	hl
	jr	ddloop

  IF 0
	; initialize physical devices

	ld	hl,PHYDEV
inidev:	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	inc	hl
	ld	a,d
	or	e		; end of list?
	jr	z,done		; exit loop if yes
	push	hl
	ld	e,l		; DE = DCB address
	ld	d,h
	inc	hl		; skip device name
	inc	hl
	inc	hl		; skip number of units
	inc	hl		; skip UCB list pointer
	inc	hl
	call	jphl		; call the driver's initialization entry point
	pop	hl
	jr	inidev
	
done:	

  ENDIF

done:	ld	de,'TT'
	ld	c,0		; 'TT0:' DE-C = physical
	ld	hl,'CO'
	ld	b,0		; 'CO0:' HL-B = logical (system console device)
	ld	a,N.GBL
	ld	ix,0
	call	ASSIGN

	ld	hl,0
	ld	(MCRTCB##),hl

	; start loader task

	ld	a,(SYSBBR##)
	ld	(LDRTDB+TD.BANK),a
	ld	hl,LDR_E
	ld	de,LDR_S
	call	npages
	ld	(LDRTDB+TD.SIZE),a
	ld	hl,LDRTDB
	call	TSTART
	ld	(LDRTCB),hl

	; start initialization task

	ld	a,(SYSBBR##)
	ld	(INITDB+TD.BANK),a
	ld	hl,INIT_E
	ld	de,INIT_S
	call	npages
	ld	(INITDB+TD.SIZE),a
	ld	hl,INITDB
	call	TSTART

	; start filesystem task (TODO: should be done by INIT or MOUNT task?)

	ld	a,(SYSBBR##)
	ld	(FCPTDB+TD.BANK),a
	ld	hl,FSRV_E
	ld	de,FSRV_S
	call	npages
	ld	(FCPTDB+TD.SIZE),a
	ld	hl,FCPTDB
	call	TSTART

	jp	GO		; start dispatcher

npages:	or	a
	sbc	hl,de
	ld	de,4095
	add	hl,de
	ld	a,h
	rrca
	rrca
	rrca
	rrca
	and	0Fh
	ret

LDRTDB:	db	'LDR...'
	db	'V6.03 '
LDRPAR:	db	'LDRPAR'
	dw	0
	db	250
	db	(1 SHL TA.PRV) OR (1 SHL TA.FIX)
	db	1,1
	db	'CO',0
	db	'LB',0
	dw	0,0,0
	db	0,0
	dw	LDR_S,LDR_E,LDR_S,LDR_E
	dw	0,0

INITDB:	db	'INIT  '
	db	'V6.03 '
SYSPAR:	db	'SYSPAR'
	dw	0
	db	200
	db	(1 SHL TA.PRV) OR (1 SHL TA.REM)
	db	1,1
	db	'CO',0
	db	'LB',0
	dw	0,0,0
	db	0,0
	dw	INIT_S,INIT_E,INIT_S,INIT_E
	dw	0,0

FCPTDB:	db	'SYSFCP'
	db	'V4.1  '
FCPPAR:	db	'FCPPAR'
	dw	0
	db	200
	db	(1 SHL TA.PRV) OR (1 SHL TA.FIX) OR (1 SHL TA.ACP)
	db	1,1
	db	'CO',0
	db	'LB',0
	dw	0,0,0
	db	0,0
	dw	FSRV_S,FSRV_E,FSRV_S,FSRV_E
	dw	0,0

GENPAR:	db	'GEN   '

;-----------------------------------------------------------------------

	dseg

	public	IDDTBL

IDDTBL:	GENDEV			; device-driver table from SYSCONF.INC
	dw	0		; end of table marker

dfhst:	DFHOST			; default host name from SYSCONF.INC

	end	START
